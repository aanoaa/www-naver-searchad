#!/usr/bin/env perl
use strict;
use warnings;

use Directory::Queue;
use FindBin qw($Bin);
use Parallel::ForkManager;
use Path::Tiny;
use Redis;
use SearchAd::Schema;
use WWW::Naver::SearchAd::Rank qw/enqueue/;

STDOUT->autoflush(1);

our $REDIS_KEY_SEARCHAD_SOCKS = 'searchad:socks';

my $config  = require "$Bin/../searchad.conf";
my $db_conf = $config->{database};
my $schema  = SearchAd::Schema->connect(
    {
        dsn      => $db_conf->{dsn},
        user     => $db_conf->{user},
        password => $db_conf->{pass},
        %{ $db_conf->{opts} },
    }
);

my @socks = path("$Bin/../socks.list")->lines( { chomp => 1 } );
@socks = grep { /^[^#]/ } @socks;

my $redis = Redis->new;
$redis->del($REDIS_KEY_SEARCHAD_SOCKS);
$redis->rpush( $REDIS_KEY_SEARCHAD_SOCKS, @socks );

my $pm = Parallel::ForkManager->new( scalar @socks );
my $dirq = Directory::Queue->new( path => '/tmp/searchad' );

while (1) {
    my $rs = $schema->resultset('Rank')->search( { on_off => 1 } );

    LOOPS:
    while ( my $r = $rs->next ) {
        $pm->start and next LOOPS;
        while (1) {
            my $socks = $redis->lpop('searchad:socks');
            unless ($socks) {
                sleep(1);
                next;
            }

            enqueue( $dirq, $r, $socks );
            $redis->rpush( 'searchad:socks', $socks );
            $pm->finish;
            last;
        }
    }

    sleep(120);
}
